plugins {
    id "io.freefair.aspectj.post-compile-weaving" version "5.3.3.3"
}

dependencies {
    implementation project(':core.api')
    inpath(project(":core.api")) {
        transitive = false
    }
    implementation (group: 'org.seleniumhq.selenium', name: 'selenium-java', version: seleniumVersion) {
        exclude group: 'org.seleniumhq.selenium', module: 'htmlunit-driver'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'net.bytebuddy', module:'byte-buddy'
    }
    implementation (group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.0.1') {
        exclude group: 'org.seleniumhq.selenium'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'org.jsoup', module: 'jsoup'
    }
    implementation (group: 'org.seleniumhq.selenium', name: 'selenium-server', version: seleniumVersion) {
        exclude group: 'org.seleniumhq.selenium', module: 'htmlunit-driver'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'net.bytebuddy', module:'byte-buddy'
    }
    testImplementation  group: 'org.mockito', name: 'mockito-core', version: '3.7.7'
    implementation group: 'cglib', name: 'cglib', version: '3.3.0'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.14.2'
    implementation group: 'com.browserup', name: 'browserup-proxy-core', version: '2.1.2'
    implementation group: 'org.objenesis', name: 'objenesis', version: '3.2'
}

test {
    useTestNG() {
        // runlist to executed. path is relative to current folder
        suites 'src/test/resources/suite.xml'
    }
}

task generateBundle(type: JavaExec)  {
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "selenium"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'selenium'
            from components.java
            repositories {
                maven {
                    credentials(PasswordCredentials)
                    url = 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa'
                }
            }
            pom {
                packaging = 'jar'
                name = 'selenium'
                version = version
                url = 'https://github.com/TinkoffCreditSystems/neptune'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'TikhomirovSergey'
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    developerConnection = 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    url = 'https://github.com/TinkoffCreditSystems/neptune'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../../neptune.documentation/javadocs/$project.name")

    title = "Neptune Selenium Integration API. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2021 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
    options.addBooleanOption('html5', true)
}