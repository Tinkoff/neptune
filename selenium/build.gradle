dependencies {
    compile project(':core.api')
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: seleniumVersion
    compile (group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '3.8.1') {
        exclude group: 'org.seleniumhq.selenium'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'org.jsoup', module: 'jsoup'
    }
    compile (group: 'org.seleniumhq.selenium', name: 'selenium-server', version: seleniumVersion) {
        exclude group: 'org.seleniumhq.selenium', module: 'htmlunit-driver'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'net.bytebuddy', module:'byte-buddy'
    }
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    compile group: 'cglib', name: 'cglib', version: '3.3.0'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
}

test {
    useTestNG()
    jvmArgs '-Xmx20484m', '-XX:MaxPermSize=1024m', '-XX:+HeapDumpOnOutOfMemoryError'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'http://nexus.tcsbank.ru/content/repositories/tcs-qa') {
                authentication(userName: mavenDeployLogin, password: mavenDeployPassword)
            }


            pom.project {
                packaging 'jar'
                name 'selenium'
                version version
                url 'https://github.com/TinkoffCreditSystems/neptune'
                developers {
                    developer {
                        name 'Sergey Tikhomirov'
                        email 'tichomirovsergey@gmail.com'
                        url 'https://github.com/TikhomirovSergey'
                        id 'TikhomirovSergey'
                    }
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/TinkoffCreditSystems/neptune'
                    connection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    developerConnection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    tag 'HEAD'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../../neptune.documentation/javadocs/$project.name")

    title = "Neptune Selenium Integration API. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2020 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
}