plugins {
    id "io.freefair.aspectj.post-compile-weaving" version "5.3.3.3"
}

dependencies {
    compile project(':core.api')
    inpath(project(":core.api")) {
        transitive = false
    }
    compile (group: 'org.seleniumhq.selenium', name: 'selenium-java', version: seleniumVersion) {
        exclude group: 'org.seleniumhq.selenium', module: 'htmlunit-driver'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'net.bytebuddy', module:'byte-buddy'
    }
    compile (group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '4.4.3') {
        exclude group: 'org.seleniumhq.selenium'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'org.jsoup', module: 'jsoup'
    }
    compile (group: 'org.seleniumhq.selenium', name: 'selenium-server', version: seleniumVersion) {
        exclude group: 'org.seleniumhq.selenium', module: 'htmlunit-driver'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'net.bytebuddy', module:'byte-buddy'
    }
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.7.7'
    compile group: 'cglib', name: 'cglib', version: '3.3.0'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
    compile group: 'com.browserup', name: 'browserup-proxy-core', version: '2.1.2'
}

test {
    useTestNG() {
        // runlist to executed. path is relative to current folder
        suites 'src/test/resources/suite.xml'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task generateBundle(type: JavaExec)  {
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "selenium"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa') {
                authentication(userName: mavenDeployLogin, password: mavenDeployPassword)
            }


            pom.project {
                packaging 'jar'
                name 'selenium'
                version version
                url 'https://github.com/TinkoffCreditSystems/neptune'
                developers {
                    developer {
                        name 'Sergey Tikhomirov'
                        email 'tichomirovsergey@gmail.com'
                        url 'https://github.com/TikhomirovSergey'
                        id 'TikhomirovSergey'
                    }
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/TinkoffCreditSystems/neptune'
                    connection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    developerConnection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    tag 'HEAD'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../../neptune.documentation/javadocs/$project.name")

    title = "Neptune Selenium Integration API. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2021 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
}