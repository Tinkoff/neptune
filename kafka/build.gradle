configurations {
    bundleGeneration
}

dependencies {
    api(project(":core.api"))
    compileOnly group: 'org.apache.kafka', name: 'kafka-clients', version: '3.1.1'
    bundleGeneration(group: 'org.apache.kafka', name: 'kafka-clients', version: '3.1.1')
    testImplementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.3.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: mockitoVersion
}

java {
    withJavadocJar()
    withSourcesJar()
}

task generateBundle(type: JavaExec)  {
    classpath = configurations.bundleGeneration + sourceSets.main.compileClasspath
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "kafka"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kafka'
            from components.java
            repositories {
                maven {
                    credentials(PasswordCredentials)
                    url = 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa'
                }
            }
            pom {
                packaging = 'jar'
                name = 'kafka'
                version = version
                url = 'https://github.com/tinkoff/neptune'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'Andrew Charykov'
                        email = 'a.s.charykov@yandex.ru'
                        url = 'https://github.com/AndrewCharykov'
                        id = 'AndrewCharykov'
                    }
                    developer {
                        id = 'TikhomirovSergey'
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    developerConnection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    url = 'https://github.com/tinkoff/neptune'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../neptune.documentation/$project.name")

    title = "Neptune Kafka. v$globalVersion"
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2023 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
    options.addBooleanOption('html5', true)
}