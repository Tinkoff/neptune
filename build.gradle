ext {
    globalVersion = '0.20.3-ALPHA'
}

subprojects {
    group 'ru.tinkoff.qa.neptune'
    version globalVersion

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'
    apply plugin: 'java-library'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    apply plugin: "java"

    repositories {
        maven {
            url "https://repo1.maven.org/maven2/"
        }
    }

    ext {
        testNGVersion = '7.4.0'
        junit5Version = '5.8.1'
        junit5PlatformVersion = '1.8.1'
        hamcrestVersion = '2.2'
        seleniumVersion = '4.0.0'
        allureVersion = '2.16.1'
        jacksonVersion = '2.13.0'
        mockitoVersion = '4.0.0'
        springVersion = '5.3.12'
        springBootVersion = '2.5.6'
        globalVersion = version
    }

    dependencies {
        api group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        api group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
        testImplementation  group: 'org.testng', name: 'testng', version: testNGVersion
        api group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        api group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: jacksonVersion
        api group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: jacksonVersion
        api group: 'commons-validator', name: 'commons-validator', version: '1.7'
        api group: 'commons-io', name: 'commons-io', version: '2.11.0'
        api group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        api group: 'io.github.classgraph', name: 'classgraph', version: '4.8.128'
        api group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion
    }

    test {
        useTestNG()

        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
            exceptionFormat "full"
            showStackTraces true
        }
    }
}

task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    title = "Neptune API Documentation. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    destinationDir  = file("${buildDir}/../../neptune.documentation/javadocs/")
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2021 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}
