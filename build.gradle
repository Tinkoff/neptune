plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

ext {
    globalVersion = '0.25.0-ALPHA'
}

repositories {
    maven {
        url "https://repo1.maven.org/maven2/"
    }
}

subprojects {

    group 'ru.tinkoff.qa.neptune'
    version globalVersion

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        maven {
            url "https://repo1.maven.org/maven2/"
        }
    }

    ext {
        testNGVersion = '7.6.1'
        junit5Version = '5.8.2'
        junit5PlatformVersion = '1.8.2'
        hamcrestVersion = '2.2'
        seleniumVersion = '4.8.0'
        allureVersion = '2.21.0'
        jacksonVersion = '2.13.3'
        mockitoVersion = '4.8.1'
        springVersion = '5.3.20'
        springDataVersion = '2.7.0'
        springBootVersion = '2.7.0'
        bytebuddyVersion = '1.13.0'
        queryDslVersion = '5.0.0'
        globalVersion = version
    }

    dependencies {
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
        testImplementation group: 'org.testng', name: 'testng', version: testNGVersion
        implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        api group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: jacksonVersion
        api group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: jacksonVersion
        api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
        api group: 'commons-validator', name: 'commons-validator', version: '1.7'
        api group: 'commons-io', name: 'commons-io', version: '2.11.0'
        implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
        implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.154'
        testImplementation group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion
        compileOnly group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion
        implementation group: 'org.apache.tika', name: 'tika-core', version: '2.4.0'
        implementation group: 'org.objenesis', name: 'objenesis', version: '3.3'
    }

    test {
        useTestNG()

        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
            exceptionFormat "full"
            showStackTraces true
        }

        finalizedBy jacocoTestReport

        jacoco {
            excludes += [
                    'ru.tinkoff.qa.neptune.core.api.agent.HamcrestTransformParameters.HamcrestInterceptor',
                    'ru.tinkoff.qa.neptune.core.api.agent.CombinableMatchersTransformParameters.HamcrestCombinableInterceptor',
                    'ru.tinkoff.qa.neptune.check.localization.*',
                    'ru.tinkoff.qa.neptune.database.abstractions.localization.*',
                    'ru.tinkoff.qa.neptune.hibernate.localization.*',
                    'ru.tinkoff.qa.neptune.http.api.localization.*',
                    'ru.tinkoff.qa.neptune.kafka.localization.*',
                    'ru.tinkoff.qa.neptune.retrofit2.localization.*',
                    'ru.tinkoff.qa.neptune.selenium.localization.*',
                    'ru.tinkoff.qa.neptune.spring.data.localization.*',
                    'ru.tinkoff.qa.neptune.spring.mock.mvc.localization.*',
                    'ru.tinkoff.qa.neptune.spring.web.testclient.localization.*',
                    'ru.tinkoff.qa.neptune.mockito.localization.*'
            ]
        }
    }

    jacocoTestReport {

        dependsOn test

        reports {
            xml.enabled true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        classDirectories.setFrom(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/**$HamcrestCombinableInterceptor.class',
                            '**/**$HamcrestInterceptor.class',
                            '**/check/localization/**',
                            '**/database/abstractions/localization/**',
                            '**/hibernate/localization/**',
                            '**/http/api/localization/**',
                            '**/kafka/localization/**',
                            '**/retrofit2/localization/**',
                            '**/selenium/localization/**',
                            '**/spring/data/localization/**',
                            '**/spring/mock/mvc/localization/**',
                            '**/spring/web/testclient/localization/**',
                            '**/mockito/localization/**'
                    ]
            )
        })
    }
}

task jacocoOverallReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output).files.collect {
        fileTree(dir: it,
                exclude: [
                        '**/**$HamcrestCombinableInterceptor.class',
                        '**/**$HamcrestInterceptor.class',
                        '**/check/localization/**',
                        '**/database/abstractions/localization/**',
                        '**/hibernate/localization/**',
                        '**/http/api/localization/**',
                        '**/kafka/localization/**',
                        '**/retrofit2/localization/**',
                        '**/selenium/localization/**',
                        '**/spring/data/localization/**',
                        '**/spring/mock/mvc/localization/**',
                        '**/spring/web/testclient/localization/**',
                        '**/mockito/localization/**'
                ]
        )
    }
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled true
        xml.enabled true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

sonarqube {
    properties {
        property "sonar.organization", "neptune"
        property "sonar.projectKey", "neptune"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/jacocoOverallReport/jacocoOverallReport.xml"
        property "sonar.qualitygate.wait", "true"
    }
}

check.dependsOn test, jacocoOverallReport

task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    title = "Neptune API Documentation. v$globalVersion"
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    destinationDir = file("${buildDir}/../neptune.documentation/")
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2023 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}
