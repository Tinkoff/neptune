ext {
    globalVersion = '0.11.11-ALPHA'
}

subprojects {
    group 'ru.tinkoff.qa.neptune'
    version globalVersion

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    apply plugin: "java"

    repositories {
        maven {
            url "https://repo1.maven.org/maven2/"
        }
    }

    ext {
        testNGVersion = '6.14.3'
        hamcrestVersion = '2.2'
        seleniumVersion = '3.141.59'
        allureVersion = '2.13.1'
        jacksonVersion = '2.10.2'
        mavenDeployLogin = hasProperty('REPO_USER_NAME') ? REPO_USER_NAME : System.getenv('REPO_USER_NAME')
        mavenDeployPassword = hasProperty('REPO_USER_PASSWORD') ? REPO_USER_PASSWORD : System.getenv('REPO_USER_PASSWORD')
        globalVersion = version
    }

    dependencies {
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
        compile group: 'com.google.guava', name: 'guava', version: '28.2-jre'
        testCompile group: 'org.testng', name: 'testng', version: testNGVersion
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: jacksonVersion
        compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
        compile group: 'commons-io', name: 'commons-io', version: '2.6'
        compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
        compile group: 'io.github.classgraph', name: 'classgraph', version: '4.8.65'
        compile group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion
    }

    test {
        useTestNG()

        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
        }
    }
}

task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    title = "Neptune API Documentation. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    destinationDir = file("${buildDir}/../../neptune.documentation/javadocs/")
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2020 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}
