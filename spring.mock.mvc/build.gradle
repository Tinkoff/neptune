configurations {
    bundleGeneration
}

dependencies {
    api project(':core.api')
    api project(':neptune-spring-boot-starter')
    compileClasspath(group: 'org.springframework', name: 'spring-test', version: springVersion)
    compileClasspath(group: 'org.springframework', name: 'spring-web', version: springVersion)
    compileClasspath(group: 'org.springframework', name: 'spring-webmvc', version: springVersion)
    compileClasspath(group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1')

    bundleGeneration(group: 'org.springframework', name: 'spring-web', version: springVersion)
    bundleGeneration(group: 'org.springframework', name: 'spring-webmvc', version: springVersion)

    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: mockitoVersion
    testImplementation(group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1')
    testImplementation(group: 'org.springframework', name: 'spring-test', version: springVersion)
    testImplementation(group: 'org.springframework', name: 'spring-web', version: springVersion)
    testImplementation(group: 'org.springframework', name: 'spring-webmvc', version: springVersion)
    testImplementation('com.jayway.jsonpath:json-path:2.7.0')
}

java {
    withJavadocJar()
    withSourcesJar()
}

task generateDefaultBundle(type: JavaExec)  {
    classpath = configurations.bundleGeneration + sourceSets.main.compileClasspath
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args Locale.US.toString(),
            "$projectDir/src/main/resources",
            "false",
            "spring.result.matchers"
}

task generateBundle(type: JavaExec)  {
    classpath = configurations.bundleGeneration + sourceSets.main.compileClasspath
    dependsOn generateDefaultBundle
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "spring.mock.mvc"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring.mock.mvc'
            from components.java
            repositories {
                maven {
                    credentials(PasswordCredentials)
                    url = 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa'
                }
            }
            pom {
                packaging = 'jar'
                name = 'spring.mock.mvc'
                version = version
                url = 'https://github.com/tinkoff/neptune'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'TikhomirovSergey'
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    developerConnection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    url = 'https://github.com/tinkoff/neptune'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../../neptune.documentation/javadocs/$project.name")

    title = "Neptune Spring Mock MVC Integration API. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2022 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
    options.addBooleanOption('html5', true)
}