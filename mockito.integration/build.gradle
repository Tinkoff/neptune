java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api(project(":core.api"))
    compileOnly group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    compileOnly group: 'org.mockito', name: 'mockito-inline', version: mockitoVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    testImplementation('org.junit.platform:junit-platform-launcher')
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.1.1'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.1.1'
    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: bytebuddyVersion
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mockito.integration'
            from components.java
            repositories {
                maven {
                    credentials(PasswordCredentials)
                    url = 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa'
                }
            }
            pom {
                packaging = 'jar'
                name = 'mockito.integration'
                version = version
                url = 'https://github.com/tinkoff/neptune'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'TikhomirovSergey'
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    developerConnection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    url = 'https://github.com/tinkoff/neptune'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../neptune.documentation/$project.name")

    title = "Neptune Mockito Integration API. v$globalVersion"
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2023 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
    options.addBooleanOption('html5', true)
}

task generateBundle(type: JavaExec) {
    classpath = sourceSets.main.compileClasspath
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "mockito"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}