dependencies {
    api project(":jupiter.integration")
    api project(":allure.integration")
    api(group: 'io.qameta.allure', name: 'allure-junit5', version: allureVersion) {
        exclude group: 'org.aspectj', module: 'aspectjrt'
        exclude group: 'org.aspectj', module: 'aspectjweaver'
        exclude group: 'org.junit.jupiter', module: 'junit-jupiter-api'
        exclude group: 'org.junit.jupiter', module: 'junit-platform-launcher'
    }
    compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
    compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: junit5PlatformVersion

    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junit5PlatformVersion
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'allure.jupiter.bridge'
            from components.java
            repositories {
                maven {
                    credentials(PasswordCredentials)
                    url = 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa'
                }
            }
            pom {
                packaging = 'jar'
                name = 'allure.jupiter.bridge'
                version = version
                url = 'https://github.com/tinkoff/neptune'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'TikhomirovSergey'
                        name = 'Sergey Tikhomirov'
                        email = 'tichomirovsergey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    developerConnection = 'scm:git:ssh://git@github.com:tinkoff/neptune.git'
                    url = 'https://github.com/tinkoff/neptune'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../neptune.documentation/$project.name")

    title = "Neptune Integration Of Allure and JUnut 5. v$globalVersion"
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2022 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
    options.addBooleanOption('html5', true)
}