plugins {
    id "io.freefair.aspectj.post-compile-weaving" version "5.3.3.3"
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':core.api')
    inpath(project(":core.api")) {
        transitive = false
    }
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.9.0'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.27'
    testCompile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.9.0'
    testCompile group: 'com.squareup.retrofit2', name: 'converter-scalars', version: '2.9.0'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock-jre8-standalone', version: '2.29.0'
}

test {
    useTestNG()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task generateBundle(type: JavaExec)  {
    classpath sourceSets.main.runtimeClasspath
    main = "ru.tinkoff.qa.neptune.core.api.localization.ResourceBundleGenerator"
    args "ru_RU",
            "$projectDir/src/main/resources",
            "false",
            "retrofit2"
}

processResources {
    doLast {
        generateBundle.exec()
    }
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'https://nexus-new.tcsbank.ru/repository/mvn-bigops-qa') {
                authentication(userName: mavenDeployLogin, password: mavenDeployPassword)
            }


            pom.project {
                packaging 'jar'
                name 'retrofit2'
                version version
                url 'https://github.com/TinkoffCreditSystems/neptune'
                developers {
                    developer {
                        name 'Sergey Tikhomirov'
                        email 'tichomirovsergey@gmail.com'
                        url 'https://github.com/TikhomirovSergey'
                        id 'TikhomirovSergey'
                    }
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/TinkoffCreditSystems/neptune'
                    connection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    developerConnection 'scm:git:ssh://git@github.com:TinkoffCreditSystems/neptune.git'
                    tag 'HEAD'
                }
            }
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/../../../neptune.documentation/javadocs/$project.name")

    title = "Neptune Retrofit 2.x Integration API. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2021 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."
}